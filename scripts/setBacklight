#!/usr/bin/fish

set value 5
if test (count $argv) -eq 1
    for device in /sys/class/backlight/*
        set current_bl (cat $device/brightness)
        set max_bl (cat $device/max_brightness)
        set min_bl (math -s0 0.05 "*" $max_bl)
        switch $argv[1]
            case get
                echo (math -s0 $current_bl / $max_bl "*" 100)"  "
            case set
                while true
                    read -fp "set_color blue; echo -en 'Enter Value of Backlight \n❱ '; set_color normal" input
                    if test -z $input
                        return 0
                    end
                    if string match -qr '^[1-9][0-9]*$' $input
                        math $input / 100 "*" $max_bl >$device/brightness
                        set_color green
                        echo -e "Backlight set to: $input\n"
                        set_color normal
                    else
                        set_color red
                        echo -e "Error: Invalid Value of Backlight\n"
                        set_color normal
                    end
                end
            case inc
                set tmp (math -s0 $current_bl + $value / 100 \* $max_bl)
                if test $tmp -ge $max_bl
                    echo $max_bl >$device/brightness
                else
                    echo $tmp >$device/brightness
                end
                # math (cat $device/brightness) / $max_bl "*" 100
            case dec
                set tmp (math -s0 $current_bl - $value / 100 \* $max_bl)
                if test $tmp -le $min_bl
                    echo $min_bl >$device/brightness
                else
                    echo $tmp >$device/brightness
                end
                # math (cat $device/brightness) / $max_bl "*" 100
            case "*"
                # math (cat $device/brightness) / $max_bl "*" 100
        end
    end
end
